#!/usr/bin/perl
use JIRA::Client::Automated;
use Term::ReadPassword;
use Encode;
use strict;

my ($url, $user, $jql) = @ARGV;
($jql && $url && $user) || die("Usage: $0 <url> <user> <JQL query> ");

$jql = Encode::decode_utf8($jql);
my $password = read_password("JIRA password for $user: ");
my $jira = JIRA::Client::Automated->new($url, $user, $password);

my $start = 0;
my $max   = 100;
binmode STDOUT, ':utf8';
while(1) {
	my $results = $jira->search_issues($jql, $start, $max);
	if ($results->{errors}) {
		die join "\n", "JIRA error: ", @{$results->{errors}};
	}
	my $issues = $results->{issues};
	foreach my $issue (@$issues) { 
		print JSON::XS->new->pretty->encode($issue);
	}	
	last if scalar(@$issues) < $max;
	$start += $max;
}


=pod

=head1 NAME

jira_search

=head1 USAGE

  jira_search <url> <user> <JQL query> 

=head1 DESCRIPTION

Dumps JIRA issues selected by JQL query. 

=head1 AUTHOR

Ivan Panchenko <panchenko@cpan.org>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2018 by the author.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut

